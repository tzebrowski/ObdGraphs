plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
    id 'com.diffplug.spotless'
}

apply from: "$project.rootDir/spotless.gradle"

android {
    compileSdk rootProject.ext.compileSdkVersion

    packaging {
        resources {
            it.excludes += '**/design.drawio'
            it.excludes += '**/highlevel.drawio'
            it.excludes += '**/highlevel.jpg'
            it.excludes += '**/component.png'
            it.excludes += '**/model.png'

            it.excludes += "META-INF/DEPENDENCIES"
            it.excludes += "META-INF/LICENSE"
            it.excludes += "META-INF/LICENSE.txt"
            it.excludes += "META-INF/license.txt"
            it.excludes += "META-INF/NOTICE"
            it.excludes += "META-INF/NOTICE.txt"
            it.excludes += "META-INF/notice.txt"
            it.excludes += "META-INF/ASL2.0"
            it.excludes += "META-INF/*.kotlin_module"
        }
    }

    defaultConfig {
        applicationId "org.obd.graphs.app"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.compileSdkVersion
        versionName new Date().format('yyyyMMdd.HHmm')
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        testFunctionalTest false
        testHandleProfiling false
        resValue "string", "DEFAULT_PROFILE", "profile_1"
        archivesBaseName = "MyGiulia.$versionName"
    }

    sourceSets {
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
        play.setRoot('build-types/release')
    }

    signingConfigs {

        debug {
            storeFile file(System.getenv("ANDROID_RELEASE_KEYSTORE"))
            storePassword System.getenv("ANDROID_RELEASE_STORE_PASSWORD")
            keyAlias System.getenv("ANDROID_RELEASE_KEY_ALIAS")
            keyPassword System.getenv("ANDROID_RELEASE_KEY_PASSWORD")
        }

        release {
            storeFile file(System.getenv("ANDROID_RELEASE_KEYSTORE"))
            storePassword System.getenv("ANDROID_RELEASE_STORE_PASSWORD")
            keyAlias System.getenv("ANDROID_RELEASE_KEY_ALIAS")
            keyPassword System.getenv("ANDROID_RELEASE_KEY_PASSWORD")
        }

        play {
            storeFile file(System.getenv("ANDROID_RELEASE_KEYSTORE"))
            storePassword System.getenv("ANDROID_RELEASE_STORE_PASSWORD")
            keyAlias System.getenv("ANDROID_RELEASE_KEY_ALIAS")
            keyPassword System.getenv("ANDROID_RELEASE_KEY_PASSWORD")
        }
    }

    flavorDimensions = ["version" ]
    productFlavors {

        giuliaAA {
            dimension "version"
            resValue "string", "DEFAULT_PROFILE", "profile_8"
            applicationId "org.obd.graphs.my.giulia.aa"
            versionCode 193
        }

        giuliaPerformanceMonitor {
            dimension "version"
            resValue "string", "DEFAULT_PROFILE", "profile_8"
            applicationId "org.obd.graphs.my.giulia.performance_monitor"
            versionCode 88
        }

        giulia {
            dimension "version"
            resValue "string", "DEFAULT_PROFILE", "profile_3"
            applicationId "org.obd.graphs.my.giulia"
            versionCode 74
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            signingConfig signingConfigs.release

            resValue "string", "ANDROID_WEB_CLIENT_ID", System.getenv("ANDROID_WEB_CLIENT_ID") ?: "no_key"
            resValue "bool", "MODULE_ANDROID_AUTO_ENABLED", "true"
            resValue "bool", "FF_READ_RESOURCE_EXTERNAL_STORAGE_ENABLED", "false"
            resValue "bool", "FF_RESULT_CACHE_ENABLED", "false"
            resValue "bool", "FF_SWITCH_NETWORK_ENABLED", "false"
            resValue "bool", "FF_SHIFT_LIGHTS_ENABLED", "false"
            resValue "bool", "DEBUG_BUILD", "false"
        }

        play {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            signingConfig signingConfigs.release

            resValue "string", "ANDROID_WEB_CLIENT_ID", System.getenv("ANDROID_WEB_CLIENT_ID") ?: "no_key"
            resValue "bool", "MODULE_ANDROID_AUTO_ENABLED", "false"
            resValue "bool", "FF_READ_RESOURCE_EXTERNAL_STORAGE_ENABLED", "false"
            resValue "bool", "FF_RESULT_CACHE_ENABLED", "false"
            resValue "bool", "FF_SWITCH_NETWORK_ENABLED", "false"
            resValue "bool", "FF_SHIFT_LIGHTS_ENABLED", "false"
            resValue "bool", "DEBUG_BUILD", "false"
        }

        debug {

            resValue "string", "ANDROID_WEB_CLIENT_ID", System.getenv("ANDROID_WEB_CLIENT_ID") ?: "no_key"
            resValue "bool", "MODULE_ANDROID_AUTO_ENABLED", "true"
            resValue "bool", "FF_READ_RESOURCE_EXTERNAL_STORAGE_ENABLED", "true"
            resValue "bool", "FF_RESULT_CACHE_ENABLED", "true"
            resValue "bool", "FF_SWITCH_NETWORK_ENABLED", "true"
            resValue "bool", "FF_SHIFT_LIGHTS_ENABLED", "true"
            resValue "bool", "DEBUG_BUILD", "true"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    lint {
        disable 'KtxExtensionAvailable', 'GradleDependency', 'ViewConstructor', 'IconLauncherShape'
    }
    namespace "org.obd.graphs"

    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation project(':screen_renderer')
    implementation project(":datalogger")
    implementation project(":common")
    implementation project(':profile')
    implementation project(':ext')
    implementation project(':integrations')

    debugImplementation project(":automotive")
    releaseImplementation project(":automotive")
    implementation project(":dragracing")

    // This specific library provides lifecycleScope
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.2"
    // (Optional) If you are using it in a ViewModel, you might also need:
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2"

    // Credential Manager (Sign In)
    implementation "androidx.credentials:credentials:1.3.0"
    implementation "androidx.credentials:credentials-play-services-auth:1.3.0"
    implementation "com.google.android.libraries.identity.googleid:googleid:1.1.1"

    // Authorization (Permissions)
    implementation "com.google.android.gms:play-services-auth:21.2.0"

    // Drive API
    implementation "com.google.api-client:google-api-client-android:2.6.0"
    implementation "com.google.apis:google-api-services-drive:v3-rev20240123-2.0.0"
    implementation 'com.google.http-client:google-http-client-android:1.44.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.0'

    implementation("androidx.activity:activity-ktx:1.9.3")
    implementation("androidx.fragment:fragment-ktx:1.8.5")

    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'com.github.tony19:logback-android:2.0.0'
    implementation 'org.apache.commons:commons-collections4:4.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.0'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.11.0'
    implementation 'io.reactivex:rxjava:1.3.8'
    implementation 'io.apisense:rhino-android:1.1.1'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'io.dropwizard.metrics:metrics-core:4.1.17'
    implementation 'com.dynatrace.dynahist:dynahist:1.1'
    implementation("io.github.tzebrowski:obd-metrics:$obdMetricVersion") { changing = true }
    implementation 'com.github.kizitonwose.colorpreference:core:1.1.0'
    implementation 'com.github.kizitonwose.colorpreference:support:1.1.0'
    debugImplementation 'com.github.DonaldDu:FixUnhandledEvent:1.0'
    implementation 'pub.devrel:easypermissions:3.0.0'

    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.5'
    androidTestImplementation 'androidx.test:core-ktx:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'

    implementation 'com.github.mik3y:usb-serial-for-android:3.5.1'

    // Import the Firebase
    implementation(platform("com.google.firebase:firebase-bom:32.1.1"))
    implementation("com.google.firebase:firebase-crashlytics")
    implementation("com.google.firebase:firebase-analytics")
}
